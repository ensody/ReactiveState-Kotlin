class Config {
    Boolean isOnCI
}

ext.setupScmVersion = { args ->
    def config = new Config(args)

    scmVersion {
        ignoreUncommittedChanges = config.isOnCI
        useHighestVersion = true
        tag {
            prefix = 'v'
            versionSeparator = ""
            deserializer { tag, position, String tagName ->
                def result = tagName.substring(1)
                while (result.count(".") < 2) {
                    result += ".0"
                }
                result
            }
            branchVersionCreator.putAll([
                'main': 'simple',
                '.*'  : { version, position ->
                    position.branch == "HEAD" ? version : "999999.0.0-${position.branch}".toString()
                },
            ])
            snapshotCreator { version, position ->
                position.branch == "main" ? "-SNAPSHOT" : ""
            }
        }
    }
    allprojects {
        version = scmVersion.version
    }
    println("Version: $version")
}
